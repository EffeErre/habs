# Maintainer: Magnus Therning <magnus@therning.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

# Todo:

pkgname=(ghc ghc-haddock)
pkgbase=ghc
pkgver=7.4.1
pkgrel=6
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
makedepends=('ghc' 'python2' 'happy' 'perl' 'libxslt' 'docbook-xsl' 'gmp>=0' 'gcc')
groups=('haskell')
source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.bz2"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.bz2"
        'fix-gen_contents_index.patch'
        'build.mk')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    cp ${srcdir}/build.mk mk/build.mk
    patch -p3 < ${srcdir}/fix-gen_contents_index.patch

    ./configure --prefix=/usr 
    make
}

check() {
    cd ghc-${pkgver}

    # python2 rename
    sed -e 's/PYTHON = python/&2/' -i testsuite/mk/boilerplate.mk
    sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

    # note: there is one test failure on i686 only: http://hackage.haskell.org/trac/ghc/ticket/5856
    make THREADS=9 test
    # might be some failures
    #make fulltest
}

package_ghc() {
    depends=('perl' 'gmp>=5.0' 'gcc' 'haddock')
    install='ghc.install'
    provides=('haskell-array=0.4.0.0'
              'haskell-base=4.5.0.0'
              'haskell-binary=0.5.1.0'
              'haskell-bin-package-db=0.0.0.0'
              'haskell-bytestring=0.9.2.1'
              'haskell-containers=0.4.2.1'
              'haskell-deepseq=1.3.0.0'
              'haskell-directory=1.1.0.2'
              'haskell-extensible-exceptions=0.1.1.4'
              'haskell-filepath=1.3.0.0'
              'haskell-ghc-prim=0.2.0.0'
              'haskell-haskell2010=1.1.0.1'
              'haskell-haskell98=2.0.0.1'
              'haskell-hoopl=3.8.7.3'
              'haskell-hpc=0.5.1.1'
              'haskell-integer-gmp=0.4.0.0'
              'haskell-old-locale=1.0.0.4'
              'haskell-old-time=1.1.0.0'
              'haskell-pretty=1.1.1.0'
              'haskell-process=1.1.0.1'
              'haskell-template-haskell=2.7.0.0'
              'haskell-time=1.4'
              'haskell-unix=2.5.1.0'
              'haskell-cabal=1.14.0')
    replaces=('haskell-array<0.4.0.0'
              'haskell-base<4.5.0.0'
              'haskell-binary<0.5.1.0'
              'haskell-bin-package-db<0.0.0.0'
              'haskell-bytestring<0.9.2.1'
              'haskell-containers<0.4.2.1'
              'haskell-deepseq<1.3.0.0'
              'haskell-directory<1.1.0.2'
              'haskell-extensible-exceptions<0.1.1.4'
              'haskell-filepath<1.3.0.0'
              'haskell-ghc-prim<0.2.0.0'
              'haskell-haskell2010<1.1.0.1'
              'haskell-haskell98<2.0.0.1'
              'haskell-hoopl<3.8.7.3'
              'haskell-hpc<0.5.1.1'
              'haskell-integer-gmp<0.4.0.0'
              'haskell-old-locale<1.0.0.4'
              'haskell-old-time<1.1.0.0'
              'haskell-pretty<1.1.1.0'
              'haskell-process<1.1.0.1'
              'haskell-template-haskell<2.7.0.0'
              'haskell-time<1.4'
              'haskell-unix<2.5.1.0'
              'haskell-cabal<1.14.0')

    cd ${srcdir}/${pkgbase}-${pkgver}

    make DESTDIR=${pkgdir} install

    # remove Haddock
    rm -fr \
        ${pkgdir}/usr/bin/haddock \
        ${pkgdir}/usr/bin/haddock-ghc-7.4.1 \
        ${pkgdir}/usr/lib/ghc-7.4.1/haddock \
        ${pkgdir}/usr/lib/ghc-7.4.1/latex/haddock.sty \
        ${pkgdir}/usr/lib/ghc-7.4.1/html

    install -d ${pkgdir}/usr/share/licenses/ghc
    install -m644 LICENSE ${pkgdir}/usr/share/licenses/ghc
}

package_ghc-haddock() {
    provides=('haddock')

    cd ${srcdir}/${pkgbase}-${pkgver}

    mkdir -p ${pkgdir}_tmp
    make DESTDIR=${pkgdir}_tmp install

    install -d ${pkgdir}/usr/bin
    mv ${pkgdir}_tmp/usr/bin/haddock ${pkgdir}/usr/bin
    mv ${pkgdir}_tmp/usr/bin/haddock-ghc-7.4.1 ${pkgdir}/usr/bin
    install -d ${pkgdir}/usr/lib/ghc-7.4.1/latex
    mv ${pkgdir}_tmp/usr/lib/ghc-7.4.1/haddock ${pkgdir}/usr/lib/ghc-7.4.1
    mv ${pkgdir}_tmp/usr/lib/ghc-7.4.1/latex/haddock.sty ${pkgdir}/usr/lib/ghc-7.4.1/latex
    mv ${pkgdir}_tmp/usr/lib/ghc-7.4.1/html ${pkgdir}/usr/lib/ghc-7.4.1

    install -d ${pkgdir}/usr/share/licenses/ghc-haddock
    install -m644 LICENSE ${pkgdir}/usr/share/licenses/ghc-haddock
}

sha256sums=('721c09a49e519b7459b67b451b5103252ba3833906a7a24c3a3679ff94669a79'
            'eea89fb679fd199c75358e6bebd754f6ef89c1e3cdb33c6a66dd919de6e3687c'
            'ae10835b3a9f5cd33ed6c50166c7d9c1cccc1eae543264273c55916e8a7f595e'
            'f4c1632516f14e3bd5c679072098848446ce65fdf44d3636eb99852256fd5f09')
